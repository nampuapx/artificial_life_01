// Последний код, который использовался для ролика " искусственная жизнь. мир 1"
// При накопление материала, код постоянно менялся, некоторые условия и команды
// удалялись, некоторые прибавлялись. 

include "constant"  // константы
include "function"  // некоторые функции, которые я вывел в отдельный файл
include "event"     // обработка кликов мышки или пальца
include "paint"     // отрисовка мира
include "bot_class" // код инструкций-генов и главнвя функция  bot_step($bot)
                    // которая выполняет код бота

save_file = 0       // сохранять ли скрин экрана
cadrov=25           // сохранять каждый .... кадр
savef=cadrov+1      // счетчик, для подсчета кадров между скринами

file_name = 1       // имя первого файла   
format = ".png"     // окончание для имен файлов
file_eman = 20000   // имя первого файла в другом режиме отображения

foo0 = 0            // переменные для отладки
foo1 = 0  
foo2 = 0  

//............  время года  .....................................
season = 11     // 11 - лето 10 - весна осень  9 - зима
                // 9, 10, и 11 используются в формуле, поэтому они такие
season_max = 10 // период каждого сезона
season_time=0   // счетчик, сколько времени прошло от начала сезона

//.....координаты для отработки касаний  ..............................................
   X_COORD0=0    Y_CO0RD0=0    C_C0ORD=0   X_centr=89  Y_centr=48

//.... переменные для отображения мира на экране ...................................... 
   ZOOM=4       // размер бота 4 или 12 или 24 пикселя
   TP  = 0      // верхний уровень мира, который виден на экране
   LFT = 0      // смещение 0 координаты Х от левого края в клетках
   age0 = 0
   age1 = 0
   age_season = 0
   PaintState=1  // отображать ли анимацию на экране
   ViewState=0   // режим отображения
//.... боты со всеми параметрами и мозгом-геномом  ....................................
   bots=new( MAX_BOTS_COUNT , BOT_SIZE ,INT16)      
   clean( bots, 0 )   

//.... карта мира  .....................................................................
// этот двумерный массив отображает карту мира. В её элементах записанно:
//  0 (WC_EMPTY)-пустая клетка, либо 2000(WC_WALL)-стена, либо ссылка на бота в массиве bots
   world=new( WORLD_WIDTH, WORLD_HEIGHT + 2 ,INT16) // сверху и снизу есть дополнительных 2 ряда, которые не отображаются
   clean( world, WC_EMPTY ) // вначале заполним карту пустотой                       
   x=0
   while x < WORLD_WIDTH    // и поставим стену сверху и снизу
    {                       // сбоков карта открыта и замкнута в кольцо
      world[ x,0 ] = WC_WALL
      world[ x,97] = WC_WALL
      x = x + 1
    }

//...........................................................................
    BOT = 0 // номер текущего бота
    generate_Adam() // запускаем первого бота
	
//...........................................................................
//..............    главный цикл    .........................................    
new_circle:
    event()   // проверка кликов на экране 
    if ( PaintState == 1 )        //если режим анимации включён,
	{ paint() frame() }           //  то отображаем на экране
    if save_file >0               // если нужно сохранить скрин
    {
      savef= savef-1              // уменьшаем счетчик
      if savef==0 { savePNG() }   // если он дошёл до нуля, то сохраняем
    }
	 // в массиве bots[] собранны все боты. Бот в нулевой ячеёке - виртуальный, он не учавствует в симуляции
	 // он ссылается на первого, реального бота, а последний бот в цепочке ссылается опять на нулевой бот.
     BOT = bots[0,NEXT]           // получаем указатель на первого бота
     bot_count=0                  // счетчик ботов
	 
//************************ выполнение кода всех ботов ********************************	 
bs:   
    bot_count=bot_count+1       
    BOT = bot_step(BOT)         // выполняется код бота и получается ссылка на следующего
    if  BOT != 0                // если ссылка не на нулевой бот, выполняем код
	    { go bs }               // следующего бота
//************************************************************************************	
	
    else      // если мы здесь, значит выполнили код всех ботов
	    {
            age0 = age0+1      // считаем, сколько ходов прошло
            if age0 == 1000 
			{ 
			    age0=0         // используется два счетчика 
				age1=age1+1    // счетчик килоХодов
                season_time = season_time + 1  // смена времени года
				if season_time == season_max
				{
				    season_time = 0
					age_season = age_season + 1
					seasonTemp=mod(age_season,4)
				    if (seasonTemp == 0)     { season = 11 }  // лето
				    else
				    {
					    if (seasonTemp == 2) { season = 9 }  // зима
					    else                 { season = 10 }  // осень весна
					}
			    }  
		    }
			radiation()      // необязательная штука. Радиация в самом верхнем слое, которая портит один ген
            go new_circle    // переходим в начало главного цикла
        }
//здесь нас быть не должно :)
  print("how are you here?",0,0)  //debug
  frame(5000)
